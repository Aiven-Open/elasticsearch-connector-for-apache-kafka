/*
 * Copyright 2019 - 2021 Aiven Oy
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {

    // https://docs.gradle.org/current/userguide/java_library_plugin.html
    id "java-library"

    // https://docs.gradle.org/current/userguide/checkstyle_plugin.html
    id "checkstyle"

    // https://docs.gradle.org/current/userguide/jacoco_plugin.html
    id "jacoco"

    // https://docs.gradle.org/current/userguide/distribution_plugin.html
    id "distribution"

    // https://docs.gradle.org/current/userguide/publishing_maven.html
    id "maven-publish"

}

repositories {
    mavenCentral()
}

group = "io.aiven"

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    withJavadocJar()
    withSourcesJar()
}

wrapper {
    distributionType = "ALL"
    doLast {
        def sha256Sum = new String(new URL("${distributionUrl}.sha256").bytes)
        propertiesFile << "distributionSha256Sum=${sha256Sum}\n"
        println "Added checksum to wrapper properties"
    }
}

ext {
    kafkaVersion = "2.2.0"
    slf4jVersion = "2.0.12"
    elasticJavaClientVersion = "8.15.2"
    testContainersElasticVersion = "1.20.1"
    carrotsearchVersion = "2.8.1"
}

compileJava {
    options.compilerArgs = ["-Xlint:all", "-Werror"]
}

checkstyle {
    toolVersion "9.2.1"
    getConfigDirectory().set(rootProject.file("checkstyle/"))
}

jacoco {
    toolVersion = "0.8.7"
}

dependencies {
    compileOnly("org.apache.kafka:connect-api:$kafkaVersion")
    compileOnly("org.apache.kafka:connect-json:$kafkaVersion")

    implementation("org.slf4j:slf4j-api:$slf4jVersion")
    implementation("co.elastic.clients:elasticsearch-java:$elasticJavaClientVersion")

    testImplementation("junit:junit:4.13.2") {
        exclude(group: "org.hamcrest", module: "hamcrest-core")
    }
    testImplementation("org.hamcrest:hamcrest-all:1.3")
    testImplementation("org.mockito:mockito-core:5.13.0")
    testImplementation("org.mockito:mockito-all:1.10.19")

    testImplementation("org.apache.kafka:connect-json:$kafkaVersion")
    testImplementation("org.testcontainers:elasticsearch:$testContainersElasticVersion")
    testImplementation("com.carrotsearch.randomizedtesting:randomizedtesting-runner:$carrotsearchVersion")
    testRuntimeOnly("org.slf4j:slf4j-log4j12:$slf4jVersion")
}

distributions {
    main {
        contents {
            from jar
            from configurations.runtimeClasspath

            into("/") {
                from projectDir
                include "version.txt", "README*", "LICENSE*", "NOTICE*", "licenses/"
                include "config/"
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            // Defaults, for clarity
            groupId = getGroup()
            artifactId = getName()
            version = getVersion()

            pom {
                name = "Aiven's Elasticsearch Sink Connector for Apache Kafka"
                description = "A Kafka Connect sink connector for copying data from Kafka to Elasticsearch."
                url = "https://aiven.io"
                organization {
                    name = "Aiven Oy"
                    url = "https://aiven.io"
                }
                licenses {
                    license {
                        name = "Apache License 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.html"
                        distribution = "repo"

                    }
                }
                scm {
                    connection = "scm:git:git://github.com/aiven/elasticsearch-connector-for-apache-kafka.git"
                    developerConnection = "scm:git:git@github.com:aiven/elasticsearch-connector-for-apache-kafka.git"
                    url = "https://github.com/aiven/elasticsearch-connector-for-apache-kafka.git"
                    tag = "HEAD"
                }
            }
        }
    }
}

processResources {
    filesMatching("elasticsearch-connector-for-apache-kafka-version.properties") {
        expand(version: version)
    }
}

jar {
    manifest {
        attributes(
                'Version': "${project.version}"
        )
    }
}

def elasticsearch7Test = tasks.register("elasticsearch7Test", Test) {
    environment("ELASTIC_TEST_CONTAINER_VERSION", "7.17.0")
}

tasks.named("check") {
    dependsOn elasticsearch7Test
}
